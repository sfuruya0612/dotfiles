#               _
#       _______| |__  _ __ ___
#      |_  / __| '_ \| '__/ __|
#     _ / /\__ \ | | | | | (__
#    (_)___|___/_| |_|_|  \___|
#

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ -f ~/.p10k.zsh ]] && source ~/.p10k.zsh

# Command alias -----------------------
# list files
alias ls="gls --color=auto -F"
alias ll="ls -aFGl"
# history
alias history="history -iD"
# vim path intall from homebrew
alias vim="`brew --prefix`/bin/nvim"
# view command
alias view="vim -R"
# ctags
alias tag="`brew --prefix`/bin/ctags"
# change direcotry to git root direcotry
alias gcd='cd $(git rev-parse --show-toplevel)'
# git status
alias gs='git status'
# git diff
alias gd='git diff'

# fzf + history
fzf_history() {
  BUFFER=$(history -n -r 1 | cut -d " " -f6- | fzf --no-sort +m --query "$LBUFFER" --prompt=">>> ")
  CURSOR=$#BUFFER
}
zle -N fzf_history
bindkey '^r' fzf_history

# fzf + export AWS_PROFILE
fa() {
    local awsProfile

    awsProfile=`grep "\[profile" ~/.aws/config | sed 's/^\[profile\ \(.*\)\]$/\1/g' | fzf --no-sort +m --prompt=">>> "`

    if [ "${awsProfile}" = "" ]; then
        return 1
    fi

    echo "Set environment variable AWS_PROFILE=${awsProfile}"
    export AWS_PROFILE=${awsProfile}
}

# fzf + ssh login
fs() {
    local sshLoginHost

    sshLoginHost=`grep -iE "^host" ~/.ssh/config | awk '{print $2}' | fzf +m --prompt=">>> "`
    if [ "${sshLoginHost}" = "" ]; then
        return 1
    fi

    ssh ${sshLoginHost}
}

# fzf + git checkout <local branch>
fgb() {
    git checkout $(git branch -a | tr -d " " | fzf --height 100% --prompt ">>> " --preview "git log --color=always {}" | head -n 1 | sed -e "s/^\*\s*//g" | perl -pe "s/remotes\/origin\///g")
}

# fzf + git log --graph
fgl() {
    git log --graph --color=always \
        --format="%C(auto)%h%d %s %C(black)%C(bold)%cr" "$@" |
    fzf --ansi --no-sort --tiebreak=index --bind=ctrl-s:toggle-sort --prompt=">>> " \
        --bind "ctrl-m:execute:
                (grep -o '[a-f0-9]\{7\}' | head -1 | xargs -I % sh -c 'git show --color=always % | less -R') << 'FZF-EOF'
                {}
                FZF-EOF"
}

# fzf + git add
fga() {
    local selected list

    selected=$(unbuffer git status -s | fzf -m --ansi --preview="echo {} | awk '{print \$2}' | xargs git diff --color" --prompt=">>> " | awk '{print $2}')

    if [[ -n "${selected}" ]]; then
        selected=$(tr '\n' ' ' <<< "${selected}")
        git add `echo ${selected} | sed 's/\s*$//'`

        git status --short
    fi
}

# fzf + git checkout
fgc() {
    local selected list

    selected=$(unbuffer git status -s "^\ M" | fzf -m --ansi --preview="echo {} | awk '{print \$2}' | xargs git diff --color" --prompt=">>> " | awk '{print $2}')

    if [[ -n "${selected}" ]]; then
        selected=$(tr '\n' ' ' <<< "${selected}")
        git checkout `echo ${selected} | sed 's/\s*$//'`

        git status --short
    fi
}

# Google Search by Safari
google() {
    local str opt

    if [ $# != 0 ]; then
        for i in $*; do
            # $strが空じゃない場合、検索ワードを+記号でつなぐ(and検索)
            str="${str}${str:++}$i"
        done
        opt='search?num=100'
        opt="${opt}&q=${str}"
    fi

    open -a Safari.app https://www.google.co.jp/${opt}
}

# Ctrl+z foreground/background switch process
fancy-ctrl-z () {
  if [[ $#BUFFER -eq 0 ]]; then
    BUFFER="fg"
    zle accept-line
  else
    zle push-input
    zle clear-screen
  fi
}
zle -N fancy-ctrl-z
bindkey '^Z' fancy-ctrl-z

# fzf-cdr
fzf-cdr() {
    target_dir=`cdr -l | sed 's/^[^ ][^ ]*  *//' | fzf`
    target_dir=`echo ${target_dir/\~/$HOME}`
    if [ -n "$target_dir" ]; then
        cd $target_dir
    fi
}
alias cdd='fzf-cdr'

# Path --------------------------------
# asdf path
export ASDF_ROOT=${HOME}/.asdf
export PATH=${ASDF_ROOT}/bin:${PATH}
source ${ASDF_ROOT}/asdf.sh
source ${ASDF_ROOT}/completions/asdf.bash

# Go path
export GOPATH=${HOME}/go
export PATH=${PATH}:${GOPATH}/bin

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
